apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'me.mattstudios.utils'
archivesBaseName = "matt-framework-gui"
version = '1.0.1-BETA'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.15.1-R0.1-SNAPSHOT'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

// General testing only
/*jar {
    destinationDir = file("C:/Users/xpsyk/Documents/MattServers/MT-15-1/plugins")
}*/

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name = 'Matt Framework GUI'
                packaging = 'jar'
                description = 'Lib for easier creation of GUIs in Spigot'
                url = 'https://github.com/ipsk/MF-GUI'

                scm {
                    connection = 'scm:git:git://github.com/ipsk/MF-GUI.git'
                    developerConnection = 'scm:git:ssh://github.com:ipsk/MF-GUI.git'
                    url = 'http://github.com/ipsk/MF-GUI'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'matt-studios'
                        name = 'Mateus Moreira'
                        email = 'xpsykogfx@gmail.com'
                    }
                }
            }
        }
    }
}
